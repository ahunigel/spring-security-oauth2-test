plugins {
    id "org.sonarqube" version "6.2.0.5505"
    id "io.freefair.lombok" version "8.13.1"
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

sourceCompatibility = 17

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/release" }
    //maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url "https://repo.spring.io/milestone" }
    //maven { url "https://repo.spring.io/snapshot" }
}

ext {
    springBootVersion = '3.4.5'
    springCloudVersion = '2024.0.1'
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")

    implementation "org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE"
    implementation "org.springframework.security:spring-security-oauth2-client:6.5.3"
    api "org.springframework.boot:spring-boot-starter-test"
    api "org.springframework.security:spring-security-test"
    api group: 'commons-beanutils', name: 'commons-beanutils', version: '1.10.1'

    implementation group: 'junit', name: 'junit'

    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.1.0'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ahunigel/${project.name}")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            
            pom {
                name = project.name
                description = 'spring security oauth2 test toolkit'
                url = "https://github.com/ahunigel/${project.name}"
                developers {
                    developer {
                        id = 'ahunigel'
                        name = 'Nigel Zheng'
                        email = 'ahunigel@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:ahunigel/${project.name}.git"
                    url = "https://github.com/ahunigel/${project.name}"
                }
            }
        }
    }
}
